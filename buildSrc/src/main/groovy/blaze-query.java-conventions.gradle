plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'com.diffplug.spotless'
}

repositories {
    mavenLocal()
    mavenCentral()
}

group = 'com.blazebit'

java {
    sourceCompatibility = 17
    targetCompatibility = 17

    withSourcesJar()
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)

            pom {
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Blazebit/blaze-query'
                    developerConnection = 'scm:git:ssh://github.com/Blazebit/blaze-query'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("OSSRH_USERNAME")
                password = System.getenv("OSSRH_PASSWORD")
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc).configureEach {
    options.encoding = 'UTF-8'
}

tasks.compileJava.dependsOn tasks.spotlessApply

spotless {
    //Don't fail during the check: rather than enforcing guidelines, we use this plugin to fix mistakes automatically.
    enforceCheck false
    java {
        targetExclude("**/build/**/*.java")
        licenseHeaderFile rootProject.file('spotless.license.java')
        removeUnusedImports()
        indentWithTabs(4)
        trimTrailingWhitespace()
        endWithNewline()
    }
}
